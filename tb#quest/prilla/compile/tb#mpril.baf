IF
  Die()
THEN
    RESPONSE #100
             DisplayString(Myself,@999998)
             ReputationInc(2)
             SetGlobal("d0change","global",2)
             DisplayString(Player1,@999999)
             ActionOverride("tb#mpri1",DestroySelf())
END

IF
  NumTimesTalkedTo(0)
  !See([PC])
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
    	     NoAction()
END

IF
  NumTimesTalkedTo(0)
  See([PC])
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
    	     StartCutSceneMode()
    	     CreateCreatureObject("tb#kobol",Myself,0,0,0)
    	     Wait(1)
             StartDialogNoSet(Player1)
             EndCutSceneMode()
END

IF
  Dead("tb#kobol")
  Global("tb#mprillacursed","global",0)
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
    	     StartCutSceneMode()
    	     CreateCreatureObject("mindfl01",Myself,0,0,0)
    	     ActionOverride("mindfl01",FaceObject("tb#mpril"))
             Wait(1)
             ActionOverride("mindfl01",SwingOnce())
             ActionOverride("mindfl01",SwingOnce())
    	     ActionOverride("mindfl01",SwingOnce())
    	     ActionOverride("mindfl01",SwingOnce())
    	     SetGlobal("tb#mprillacursed","global",1)
    	     SetGlobalTimer("tb#mprillacurses","global",6)
    	     Wait(10)
    	     ActionOverride("mindfl01",DestroySelf())
	     EndCutSceneMode()
END
IF
  OR(2)
       GlobalTimerNotExpired("tb#mprillacurses","global")
       !See([PC])
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
    	     MoveToObjectFollow(Player1)
END

IF
  GlobalTimerExpired("tb#mprillacurses","global")
  See([PC])
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
	     IncrementGlobal("tb#legalaction","global",1)
	     StartDialogueNoSet(Player1)
    	     SetGlobalTimer("tb#mprillacurses","global",6)
END

IF
  Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
    	     NoAction()
END

IF
  Global("prep","locals",0)
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999989)
             DisplayString(Myself,@999989)
             SetGlobalTimer("pfmw","locals",24)
             ReallyForceSpell(Myself,WIZARD_MINOR_SPELL_TURNING)
             ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)

             DisplayString(Myself,@999991)
             CreateCreatureObject("tb#mpri1",Myself,0,0,0)

             DisplayString(Myself,@999997)
             ReallyForceSpell(Myself,WIZARD_MONSTER_SUMMONING_1)

             DisplayString(Myself,@999993)
             SetGlobalTimer("truesight","locals",60)
             ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)

             DisplayString(Myself,@999986)
             ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_GREATER_MALISON)

             DisplayString(Myself,@999984)
             ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
             ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
             ReallyForceSpell(Myself,WIZARD_BLUR)
             ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)

    	     DisplayString(Myself,@999978)
             CreateCreatureObject("troll01",Myself,0,0,0)

             DisplayString(Myself,@999982)
		CreateVisualEffectObject("SPPROIMG",Player1)
		CreateVisualEffectObject("SPPROIMG",Player2)
		CreateVisualEffectObject("SPPROIMG",Player3)
		CreateVisualEffectObject("SPPROIMG",Player4)
		CreateVisualEffectObject("SPPROIMG",Player5)
		CreateVisualEffectObject("SPPROIMG",Player6)
		Wait(2)
		CreateCreatureObjectCopy("jclone01",Player1,0,0,0) // Calahan
		CreateCreatureObjectCopy("jclone01",Player2,0,0,0) // Calahan
		CreateCreatureObjectCopy("jclone01",Player3,0,0,0) // Calahan
		CreateCreatureObjectCopy("jclone01",Player4,0,0,0) // Calahan
		CreateCreatureObjectCopy("jclone01",Player5,0,0,0) // Calahan
		CreateCreatureObjectCopy("jclone01",Player6,0,0,0) // Calahan
             SetGlobalTimer("cast","locals",6)
             SetGlobal("prep","locals",1)
END

IF
  !See([PC])
  !GlobalTimerNotExpired("cast","locals")
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999994)
             DisplayString(Myself,@999994)
             ReallyForceSpell(Player1,WIZARD_DIMENSION_DOOR)
             SetGlobalTimer("cast","locals",6)
END

IF
  !GlobalTimerNotExpired("pfmw","locals")
  !GlobalTimerNotExpired("cast","locals")
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999989)
             DisplayString(Myself,@999989)
             SetGlobalTimer("pfmw","locals",24)
             ReallyForceSpell(Myself,WIZARD_MINOR_SPELL_TURNING)
             ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
             SetGlobalTimer("cast","locals",6)
END

IF
  !GlobalTimerNotExpired("cast","locals")
  !GlobalTimerNotExpired("truesight","locals")
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999993)
             DisplayString(Myself,@999993)
             SetGlobalTimer("truesight","locals",60)
             ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
             SetGlobalTimer("cast","locals",6)
END

IF
  !GlobalTimerNotExpired("cast","locals")
  !GlobalTimerNotExpired("deathsp","locals")
  See([GOODCUTOFF.0.0.0.0.SUMMONED])
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999987)
             DisplayString(Myself,@999987)
             SetGlobalTimer("cast","locals",6)
             SetGlobalTimer("deathsp","locals",24)
             ReallyForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END

IF
  !GlobalTimerNotExpired("cast","locals")
  !GlobalTimerNotExpired("monster1","locals")
  !See("toll01")
THEN
    RESPONSE #100
    	     DisplayStringHead(Myself,@999978)
    	     DisplayString(Myself,@999978)
             CreateCreatureObject("troll01",Myself,0,0,0)
             SetGlobalTimer("cast","locals",6)
             SetGlobalTimer("monster1","locals",24)
END

IF
  !GlobalTimerNotExpired("cast","locals")
  !GlobalTimerNotExpired("monster","locals")
  !See([EVILCUTOFF.0.0.0.0.SUMMONED])
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999997)
             DisplayString(Myself,@999997)
             SetGlobalTimer("cast","locals",6)
             ReallyForceSpell(Myself,WIZARD_MONSTER_SUMMONING_1)
             SetGlobalTimer("monster","locals",24)
END

IF
  !GlobalTimerNotExpired("cast","locals")
  !GlobalTimerNotExpired("gmal","locals")
THEN
    RESPONSE #100
             DisplayStringHead(Myself,@999986)
             DisplayString(Myself,@999986)
             SetGlobalTimer("cast","locals",6)
             ReallyForceSpell(Myself,WIZARD_GREATER_MALISON)
             SetGlobalTimer("gmal","locals",240)
END

IF
  !GlobalTimerNotExpired("cast","locals")
THEN
    RESPONSE #100
             DisplayString(Myself,@999995)
             DisplayStringHead(Myself,@999995)
             ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHROMATIC_ORB)
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
             DisplayString(Myself,@999992)
             DisplayStringHead(Myself,@999992)
             ReallyForceSpellRES("tb#lvldr",NearestEnemyOf(Myself))
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
             DisplayString(Myself,@999990)
             DisplayStringHead(Myself,@999990)
             ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_EMOTION_HOPELESSNESS)
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
             DisplayString(Myself,@999988)
             DisplayStringHead(Myself,@999988)
             ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FEEBLEMIND)
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
             DisplayString(Myself,@999985)
             DisplayStringHead(Myself,@999985)
             ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DIRE_CHARM)
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
    	     DisplayStringHead(Myself,@999976)
    	     DisplayString(Myself,@999976)
    	     ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_POLYMORPH_OTHER)
             SetGlobalTimer("cast","locals",6)
    RESPONSE #100
    	     DisplayStringHead(Myself,@999977)
    	     DisplayString(Myself,@999977)
    	     ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_RAY_OF_ENFEEBLEMENT)
             SetGlobalTimer("cast","locals",6)
END

IF
  GlobalTimerNotExpired("cast","locals")
  Delay(1)
THEN
    RESPONSE #100
             DisplayString(Myself,@999996)
             DisplayStringHead(Myself,@999996)
             AttackOneRound(NearestEnemyOf(Myself))
END

